buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'http://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'javassist:javassist:3.12.1.GA'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'

group = project.modGroup
version = project.modVersion

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = project.forgeVersion
    runDir = 'run'
    mappings = project.mcpVersion
    coreMod = 'club.cafedevelopment.kotlinbase.KotlinBase'
    makeObfSourceJar = false
}

repositories {
    maven { url = 'http://repo.spongepowered.org/maven/' }
    maven { url = "https://jitpack.io" }
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'club.minnced:java-discord-rpc:2.0.1' // RPC.
    compile 'com.github.CafeDevelopment:Blitz:2.0-SNAPSHOT' // Blitz.
    compile 'com.github.CafeDevelopment:Venture:2.0-SNAPSHOT' // Venture.
    compile 'com.googlecode.json-simple:json-simple:1.1.1' // Json.
    compile 'javassist:javassist:3.12.1.GA' // Javassist.
    compile('org.reflections:reflections:0.9.12') { exclude group: "javassist" } // Reflections.
    compile('org.spongepowered:mixin:0.7.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    } // Mixins.
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version" // Kotlin.
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    dependencies {
        include(dependency('org.jetbrains.kotlin:kotlin-stdlib-jdk8'))
        include(dependency('org.spongepowered:mixin'))
        include(dependency('club.minnced:java-discord-rpc:2.0.1'))
        include(dependency('com.github.CafeDevelopment:Blitz:2.0-SNAPSHOT'))
        include(dependency('com.github.CafeDevelopment:Venture:2.0-SNAPSHOT'))
        include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
        include(dependency('org.reflections:reflections:0.9.12'))
        include(dependency('javassist:javassist'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'release'
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.kotlinbase.refmap.json'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

jar {
    manifest.attributes(
            'Main-Class': project.mainClass,
            'MixinConfigs': 'mixins.kotlinbase.json',
            'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': project.mainClass,
            'ForceLoadAsMod': 'true',
            'FMLAT': 'kotlinbase_at.cfg'
    )
}

startScripts {
    mainClassName = project.mainClass
}

build.dependsOn(shadowJar)

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}